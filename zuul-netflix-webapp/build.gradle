apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'jetty'

dependencies {

    compile project(':zuul-core')
    compile project(':zuul-netflix')

    compile 'com.netflix.karyon:karyon-core:2.0.02'
    compile 'com.netflix.karyon:karyon-admin:2.0.02'
    compile 'com.netflix.karyon:karyon-admin-web:2.0.02'
    compile 'com.netflix.karyon:karyon-extensions:2.0.02'
    compile 'com.netflix.blitz4j:blitz4j:1.35'
    compile 'com.sun.jersey:jersey-core:1.17.1'
    compile 'com.sun.jersey:jersey-server:1.17.1'
    compile 'com.sun.jersey:jersey-client:1.17.1'
    compile 'com.sun.jersey:jersey-servlet:1.17.1'

    groovy "org.codehaus.groovy:groovy-all:2.2.2"
    provided 'junit:junit-dep:4.10'
}

javadoc {
    options {
        doclet = "org.benjchristensen.doclet.DocletExclude"
        docletpath = [rootProject.file('./gradle/doclet-exclude.jar')]
        stylesheetFile = rootProject.file('./gradle/javadocStyleSheet.css')
        windowTitle = "Zuul Javadoc ${project.version}"
    }
}

eclipse {
    classpath {
        plusConfigurations += configurations.provided
        downloadSources = true
        downloadJavadoc = true
    }
}


idea {
    module {
        languageLevel = '1.7'
        scopes.PROVIDED.plus += configurations.provided
    }
}

war {
    webXml = file('src/main/webapp/WEB-INF/web.xml')
    webInf{
        from('src/main/groovy/filters') {
            into('classes')
        }
    }
}

jettyRun.contextPath = '/'

jettyRun.doFirst {
    System.setProperty("archaius.deployment.applicationId", "zuul")
}
