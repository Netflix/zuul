plugins {
    id 'com.netflix.nebula.netflixoss' version '11.3.1'
    id "com.google.osdetector" version "1.7.3"
    id "me.champeau.jmh" version "0.7.1"
    id "org.openrewrite.rewrite" version "6.1.4"
}

ext.githubProjectName = rootProject.name


idea {
    project {
        languageLevel = '1.8'
    }
}

configurations.all {
    exclude group: 'asm', module: 'asm'
    exclude group: 'asm', module: 'asm-all'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'com.netflix.nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'com.netflix.nebula.javadoc-jar'
    apply plugin: 'com.netflix.nebula.dependency-lock'
    apply plugin: 'me.champeau.jmh'
    apply plugin: 'org.openrewrite.rewrite'

    license {
        ignoreFailures = false
        excludes([
                "**/META-INF/services/javax.annotation.processing.Processor",
                "**/META-INF/gradle/incremental.annotation.processors",
                "**/*.cert",
                "**/*.jks",
                "**/*.key",
        ])
    }

    group = "com.netflix.${githubProjectName}"

    sourceCompatibility = '1.8'

    eclipse {
        classpath {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    tasks.withType(Javadoc).each {
        it.classpath = sourceSets.main.compileClasspath
        // Ignore Javadoc warnings for now, re-enable after Zuul 3.
        it.options.addStringOption('Xdoclint:none', '-quiet')
    }

    ext {
        libraries = [
                guava: "com.google.guava:guava:31.1-jre",
                okhttp: 'com.squareup.okhttp3:okhttp:4.11.0',
                jupiterApi: 'org.junit.jupiter:junit-jupiter-api:5.+',
                jupiterParams: 'org.junit.jupiter:junit-jupiter-params:5.+',
                jupiterEngine: 'org.junit.jupiter:junit-jupiter-engine:5.+',
                jupiterMockito: 'org.mockito:mockito-junit-jupiter:4.+',
                mockito: 'org.mockito:mockito-core:4.+',
                slf4j: "org.slf4j:slf4j-api:1.7.36",
                truth: 'com.google.truth:truth:1.1.3',
                awaitility: 'org.awaitility:awaitility:4.2.0'
        ]
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
        }
    }
}

dependencies {
    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:2.0.1"))
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-testing-frameworks")
}

rewrite {
    failOnDryRunResults = true
    activeRecipe("org.openrewrite.java.testing.junit5.JUnit5BestPractices")
    activeRecipe("org.openrewrite.java.logging.slf4j.Slf4jBestPractices")
    exclusion(
            // Exclude all groovy files
            "**/*.groovy")
}
