buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.0'
    }
}

plugins {
    id 'nebula.netflixoss' version '3.4.0'
}

ext.githubProjectName = rootProject.name

idea {
    project {
        languageLevel = '1.7'
    }
}

configurations.all {
    // Exclude old asm 3.x versions.
    exclude group: 'asm', module: 'asm'
    exclude group: 'asm', module: 'asm-all'
}

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'nebula.provided-base'

    repositories {
        jcenter()
    }

    group = "com.netflix.${githubProjectName}"
    
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    sourceSets.test.java.srcDir 'src/main/java'

    tasks.withType(Javadoc).each {
        it.classpath = sourceSets.main.compileClasspath
    }

    test {
        forkEvery = 1
        maxParallelForks = 1
    }
}
