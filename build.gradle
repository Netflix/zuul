ext.githubProjectName = 'zuul'

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

buildscript {
    repositories { 
        mavenLocal()
        mavenCentral()
    }

    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    repositories { 
        mavenLocal()
        mavenCentral() 
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

subprojects {
    apply plugin: 'java'
    group = "com.netflix.zuul"
    
    tasks.withType(Javadoc).each {
        it.classpath = sourceSets.main.compileClasspath
    }

    // make 'examples' use the same classpath
    configurations { 
        examplesCompile.extendsFrom compile
        examplesRuntime.extendsFrom runtime
    }

    sourceSets { 
        examples
    }

    eclipse { 
        classpath { 
            downloadSources = true
            downloadJavadoc = true
        }
   }

    idea { 
        module { 
            scopes.PROVIDED.minus += configurations.compile
        }
    }

    task sourceJar(type:Jar) { 
        from sourceSets.main.allSource
        classifier = 'source'
    }

    task cleanLibs(type: Delete) {
        delete 'ide-lib'
    }

    task stageLibs(dependsOn:'cleanLibs', type: Copy) {
        into "ide-lib"
        from configurations.all
    }
}
