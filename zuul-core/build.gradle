apply plugin: "com.google.osdetector"
apply plugin: "java-library"

dependencies {

    implementation libraries.guava
    // TODO(carl-mastrangelo): this can be implementation; remove Logger from public api points.
    api libraries.slf4j
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.2'

    api "com.netflix.archaius:archaius-core:0.7.7"
    api "com.netflix.spectator:spectator-api:1.2.0"
    api "com.netflix.netflix-commons:netflix-commons-util:0.3.0"

    api project(":zuul-discovery")

    api "com.netflix.ribbon:ribbon-core:${versions_ribbon}"
    api "com.netflix.ribbon:ribbon-archaius:${versions_ribbon}"

    api "com.netflix.eureka:eureka-client:1.10.17"
    api "io.reactivex:rxjava:1.3.8"
    api platform("io.netty:netty-bom:${versions_netty}")
    // TODO(carl-mastrangelo): some of these could probably be implementation.   Do a deeper check.
    api "io.netty:netty-common"
    api "io.netty:netty-buffer"
    api "io.netty:netty-codec-http"
    api "io.netty:netty-codec-http2"
    api "io.netty:netty-handler"
    api "io.netty:netty-transport"

    implementation "io.netty:netty-codec-haproxy"
    implementation "io.netty:netty-transport-native-epoll:linux-x86_64"
    implementation "io.netty:netty-transport-native-kqueue:4.1.76.Final"

    implementation "io.netty.incubator:netty-incubator-transport-native-io_uring:${versions_netty_io_uring}:linux-x86_64"

    runtimeOnly "io.netty:netty-tcnative-boringssl-static"

    implementation 'io.perfmark:perfmark-api:0.25.0'
    implementation 'javax.inject:javax.inject:1'

    testImplementation libraries.junit,
            libraries.mockito,
            libraries.truth,
            libraries.awaitility


    testImplementation 'commons-configuration:commons-configuration:1.10'

    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.36'

    jmh 'org.openjdk.jmh:jmh-core:1.+'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.+'
}

// Silences log statements during tests.   This still allows normal failures to be printed.
test {
    testLogging {
        showStandardStreams = false
    }
}

// ./gradlew --no-daemon clean :zuul-core:jmh
jmh {
    profilers = ["gc"]
    timeOnIteration = "1s"
    warmup = "1s"
    fork = 1
    warmupIterations = 10
    iterations 5
    // Not sure why duplicate classes are aon the path.  Something Nebula related I think.
    duplicateClassesStrategy = 'EXCLUDE'
}
